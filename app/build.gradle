buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.+'
    }
}
apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt' //needed for source code generation
apply plugin: 'io.fabric'
apply plugin: 'checkstyle'
apply plugin: 'realm-android'

checkstyle {
    toolVersion = '7.1.1'
}

task checkstyle(type: Checkstyle) {
    // Cleaning the old log because of the creation of the new ones (not sure if totaly needed)
    delete fileTree(dir: "${project.rootDir}/app/build/reports/checkstyle")
    configFile file("${project.rootDir}/checkstyle.xml")
    source 'src'
    include '**/*.java'
    exclude '**/gen/**'

    reports {
        xml.enabled = true
        xml {
            destination "${project.rootDir}/app/build/reports/checkstyle/checkstyle-result.xml"
        }
    }

    classpath = files()
}

task listFlavors {
    android.productFlavors.all {
        flavor ->
            println(flavor.name)
    }
}

repositories {
    maven { url 'https://maven.fabric.io/public' }
}

android {
    compileSdkVersion 25
    buildToolsVersion '25.0.2'
    useLibrary 'org.apache.http.legacy'

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 25
        // Enabling multidex support.
        multiDexEnabled true
        vectorDrawables.useSupportLibrary = true
    }
    def FABRIC_VIDEONA_ORG_APIKEY = "eba78720f87307ca48dfa4200dd90914ff6653f2"
    def FABRIC_VIDEONA_ORG_APISECRET = "1636d8a8a8c633c8ba87f5a67ca26a5cc354c414b108ef565df202f07f393ae9"

    productFlavors {
        vimojo {
            applicationId "com.videonasocialmedia.vimojo.main"
            versionCode 24 // 20170324
            versionName "0.4.22" // 20170324
            resValue "string", "app_out_of_date", "2017-07-31" // 20170321
            resValue "string", "app_name", "Vimojo"
            buildConfigField "boolean", "FEATURE_FTP", "false"
            buildConfigField "boolean", "FEATURE_VOICE_OVER", "true"
            buildConfigField "boolean", "FEATURE_WATERMARK", "false"
            ext.fabricApiKey = "1f6610673faf2f5fcde5824176e94c8f4570aa2c"
            ext.fabricApiSecret = "c529f00d2df71a2bc5215e4a75166cec6733c4781ea9e8df57d0c752e909701d"
            manifestPlaceholders = [appName: "Vimojo", fabricApiKey: "1f6610673faf2f5fcde5824176e94c8f4570aa2c"]
        }
        hispanopost {
            applicationId "com.videonasocialmedia.vimojo.hispanopost"
            versionCode 4 // 20170324
            versionName "0.4.22.fHispanopost.1" // 20170324
            resValue "string", "app_out_of_date", "2016-12-01"
            resValue "string", "app_name", "HispanoPost"
            buildConfigField "boolean", "FEATURE_FTP", "false"
            buildConfigField "boolean", "FEATURE_VOICE_OVER", "false"
            buildConfigField "boolean", "FEATURE_WATERMARK", "false"
            ext.fabricApiKey = "3fc92dd8023e505ebab31a962e1eb284f37696bb"
            ext.fabricApiSecret = "f88950e831f1f8c6d7402b96e4e93c1c2f71f1e021dc68b183b8b3d2456b9b67"
            manifestPlaceholders = [appName: "HispanoPost", fabricApiKey: "3fc92dd8023e505ebab31a962e1eb284f37696bb"]
        }
        shoulderpod {
            applicationId "com.videonasocialmedia.vimojo.shoulderpod"
            versionCode 4 // 20170324
            versionName "0.4.22.fShoulderpod.1" // 20170324
            resValue "string", "app_out_of_date", "2016-12-01"
            resValue "string", "app_name", "Shoulderpod"
            buildConfigField "boolean", "FEATURE_FTP", "false"
            buildConfigField "boolean", "FEATURE_VOICE_OVER", "false"
            buildConfigField "boolean", "FEATURE_WATERMARK", "false"
            ext.fabricApiKey = "cee2254b437b99953c8a2a690fb3bf98cf36c343"
            ext.fabricApiSecret = "356b560c9fa95c84d8635ad0ab7049a664ebe2917dd954a5b97ebd95faee4fa8"
            manifestPlaceholders = [appName: "Shoulderpod", fabricApiKey: "cee2254b437b99953c8a2a690fb3bf98cf36c343"]
        }
        veinteminutos {
            applicationId "com.videonasocialmedia.vimojo.veinteminutos"
            versionCode 4 // 20170324
            versionName "0.4.22.f20minutos.1" // 20170324
            resValue "string", "app_out_of_date", "2016-12-13"
            resValue "string", "app_name", "Video 20M"
            buildConfigField "boolean", "FEATURE_FTP", "true"
            buildConfigField "boolean", "FEATURE_VOICE_OVER", "true"
            buildConfigField "boolean", "FEATURE_WATERMARK", "false"
            ext.fabricApiKey = "cf33bf18eadd90ff4f7f671588c2017052150cbf"
            ext.fabricApiSecret = "93b01a6ab712ca7229c35b072324a5e5e89a3c075e107932b0db61864e2ea880"
            manifestPlaceholders = [appName: "Video 20M", fabricApiKey: "cf33bf18eadd90ff4f7f671588c2017052150cbf"]
        }
        rtve {
            applicationId "com.videonasocialmedia.vimojo.rtve"
            versionCode 3 // 20170324
            versionName "0.4.22.fRtve.1" // 20170324
            resValue "string", "app_out_of_date", "2017-07-31" // 20170321
            resValue "string", "app_name", "Vimojo Rtve"
            buildConfigField "boolean", "FEATURE_FTP", "true"
            buildConfigField "boolean", "FEATURE_VOICE_OVER", "true"
            buildConfigField "boolean", "FEATURE_WATERMARK", "false"
            ext.fabricApiKey = "9600ab9fda1b0c0991396ffc882a957bbcca8678"
            ext.fabricApiSecret = "b3bf19cf86f8075700885f47ec7cf2813b8d8b6832a78a008ed9b757e5a56d91"
            manifestPlaceholders = [appName: "Vimojo Rtve", fabricApiKey: "9600ab9fda1b0c0991396ffc882a957bbcca8678"]
        }
        ochoaldia {
            applicationId "com.videonasocialmedia.vimojo.ochoaldia"
            versionCode 1 // 20170324
            versionName "0.4.22.fOchoAlDia.1" // 20170324
            resValue "string", "app_out_of_date", "2017-07-31"
            resValue "string", "app_name", "8 al día"
            buildConfigField "boolean", "FEATURE_FTP", "false"
            buildConfigField "boolean", "FEATURE_VOICE_OVER", "false"
            buildConfigField "boolean", "FEATURE_WATERMARK", "true"
            ext.fabricApiKey = "636f0d9539c15314d7f540afdd251bec68edeb01"
            ext.fabricApiSecret = "13f8e6cd589173f4405844b46bc19b5d8acca736dc0c3d963ca8c16d48bc7137"
            manifestPlaceholders = [appName: "8 al día", fabricApiKey: "636f0d9539c15314d7f540afdd251bec68edeb01"]
        }
        elpaisdigital {
            applicationId "com.videonasocialmedia.vimojo.elpaisdigital"
            versionCode 1 // 20170324
            versionName "0.4.22.fElPaisDigial.1" // 20170324
            resValue "string", "app_out_of_date", "2017-07-31"
            resValue "string", "app_name", "El País Digital"
            buildConfigField "boolean", "FEATURE_FTP", "false"
            buildConfigField "boolean", "FEATURE_VOICE_OVER", "false"
            buildConfigField "boolean", "FEATURE_WATERMARK", "false"
            ext.fabricApiKey = "9e2c54ec6e94c68364a80a8e511406d4b74285c8"
            ext.fabricApiSecret = "08a85538f0c5c0811431392c62d603bb2d93bcf6102a3f85fe614decde8a03f2"
            manifestPlaceholders = [appName: "El País Digital", fabricApiKey: "9e2c54ec6e94c68364a80a8e511406d4b74285c8"]
        }

    }

    File fabricProperties = new File("${project.projectDir.absolutePath}/fabric.properties")
    applicationVariants.all { variant ->
        variant.productFlavors.each { flavor ->
            def variantSuffix = variant.name.capitalize()
            def generatePropertiesTask = task("fabricGenerateProperties${variantSuffix}") << {
                Properties properties = new Properties()
                if (variantSuffix.endsWith("Debug")) {
                    properties.put("apiKey", FABRIC_VIDEONA_ORG_APIKEY)
                    properties.put("apiSecret", FABRIC_VIDEONA_ORG_APISECRET)
                } else {
                    properties.put("apiKey", flavor.fabricApiKey)
                    properties.put("apiSecret", flavor.fabricApiSecret)
                }
                properties.store(new FileWriter(fabricProperties), "")
                println "Generating fabric.properties for api key: ${flavor.fabricApiKey}"
            }

            def generateResourcesTask = project.tasks.getByName("fabricGenerateResources${variantSuffix}")
            generateResourcesTask.dependsOn generatePropertiesTask
//            generateResourcesTask.doLast {
//                println "Removing fabric.properties"
//                fabricProperties.delete()
//            }
            def crashlyticsUploadDistributionTask = project.tasks.getByName("crashlyticsUploadDistribution${variantSuffix}")
            crashlyticsUploadDistributionTask.dependsOn generatePropertiesTask
        }
    }

    buildTypes {
        release {
            buildConfigField "String", "MIXPANEL_TOKEN", "\"6f46145c03bf5cf5104bb3536059d994\""
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix '-DEBUG'
            resValue "string", "app_name", "ViMoJo Debug"
            ext.betaDistributionGroupAliases="videona-dev-team"
            ext.betaDistributionReleaseNotesFilePath="${project.rootDir}/RELEASE"
            buildConfigField "String", "MIXPANEL_TOKEN", "\"c6617213110cd2513dfc0ad97dbaee66\""
            ext.fabricApiKey = FABRIC_VIDEONA_ORG_APIKEY
            ext.fabricApiSecret = FABRIC_VIDEONA_ORG_APISECRET
            manifestPlaceholders = [appName: "Vimojo", fabricApiKey: FABRIC_VIDEONA_ORG_APIKEY]
        }
    }
    packagingOptions {
        exclude 'LICENSE.txt'
    }
    dexOptions {
        javaMaxHeapSize "2048M"
    }
    lintOptions {
        disable 'MissingTranslation'
        abortOnError false
    }
    testOptions {
        unitTests.all {
            jvmArgs '-noverify' // workarround to fix VerifyError caused by proguard sdk minify
            systemProperty 'robolectric.offline', 'true'
            systemProperty 'robolectric.dependency.dir', "$System.env.ROBOLECTRIC_LIB_PATH"
        }
    }
}

dependencies {
    // Dagger setup
    apt 'com.google.dagger:dagger-compiler:2.7' // needed for source code generation
    compile 'com.google.dagger:dagger:2.7' // dagger itself
    provided 'org.glassfish:javax.annotation:10.0-b28' // needed to resolve compilation errors

    testCompile 'org.hamcrest:hamcrest-library:1.3'
    testCompile 'org.hamcrest:hamcrest-core:1.3'
    testCompile('junit:junit:4.12') {
        exclude group: "org.hamcrest"
    }
    testCompile('org.mockito:mockito-core:1.10.19') {
        exclude group: "org.hamcrest"
    }
    testCompile 'org.robolectric:robolectric:3.1.2'
    testCompile 'org.powermock:powermock-api-mockito:1.6.4'
    testCompile 'org.powermock:powermock-module-junit4-rule-agent:1.6.4'
//    testCompile 'org.powermock:powermock-module-junit4-rule:1.6.4'
    testCompile 'org.powermock:powermock-module-junit4:1.6.4'
    testCompile 'org.json:json:20140107'

    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile project(':avrecorder')
    compile 'com.android.support:appcompat-v7:25.2.0'
    compile 'com.android.support:multidex:1.0.1'
    compile 'com.android.support:recyclerview-v7:25.2.0'
    compile 'com.android.support:cardview-v7:25.2.0'
    compile 'com.android.support:percent:25.2.0'
    compile 'com.android.support:support-v13:25.2.0'
    compile 'com.github.bumptech.glide:glide:3.6.0'
//    compile 'com.googlecode.mp4parser:isoparser:1.0.5.4'
    compile 'com.jakewharton:butterknife:7.0.1'
    compile 'com.squareup.okhttp:okhttp:2.0.0'
    compile 'com.squareup.okhttp:okhttp-urlconnection:2.0.0'
    compile 'de.greenrobot:eventbus:2.4.0'
    compile 'com.mixpanel.android:mixpanel-android:4.6.4'
//    compile 'com.google.android.gms:play-services-ads:8.3.0'
    compile 'com.google.android.gms:play-services-identity:8.3.0'
    compile 'com.google.android.gms:play-services-gcm:8.3.0'
    compile 'com.google.android.gms:play-services-analytics:8.3.0'
    compile 'com.android.support:design:25.1.0'
    compile 'com.getbase:floatingactionbutton:1.10.1'
    compile 'com.karumi:dexter:2.2.2'
    compile 'commons-net:commons-net:3.5'
    compile 'com.jcraft:jsch:0.1.53'
    // https://mvnrepository.com/artifact/com.jcraft/jzlib
    compile group: 'com.jcraft', name: 'jzlib', version: '1.1.1'
    compile 'org.florescu.android.rangeseekbar:rangeseekbar-library:0.3.0'
    compile 'com.google.android.exoplayer:exoplayer:r1.5.10'
    compile 'io.realm:android-adapters:1.3.0'
    compile 'com.roughike:bottom-bar:2.0.2'

//    compile(name:'videona-sdk-v0.1.0',ext:'aar')
    compile project(':sdk')

    compile 'org.jcodec:jcodec:0.1.9'

    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.4-beta2'
    testCompile "com.squareup.leakcanary:leakcanary-android-no-op:1.4-beta2"
    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.4-beta2'

    androidTestCompile 'org.hamcrest:hamcrest-library:1.3'
    androidTestCompile 'org.hamcrest:hamcrest-core:1.3'
    androidTestCompile('junit:junit:4.12') {
        exclude group: "org.hamcrest"
    }
    androidTestCompile('org.mockito:mockito-core:1.10.19') {
        exclude group: "org.hamcrest"
    }
    androidTestCompile 'com.google.dexmaker:dexmaker:1.2'
    androidTestCompile 'com.google.dexmaker:dexmaker-mockito:1.2'
    compile('com.crashlytics.sdk.android:crashlytics:2.6.3@aar') {
        transitive = true;
    }

    androidTestCompile 'junit:junit:4.12'
    androidTestCompile 'io.reactivex:rxjava:1.1.0'
    androidTestCompile 'com.android.support.test:runner:0.4.1'
    androidTestCompile 'com.android.support.test:rules:0.4.1'
    androidTestCompile 'com.android.support:support-annotations:25.2.0'

}
